extends RigidBody2D

var screensize = Vector2.ZERO
var size
var radius
# ğŸ‘‡ is multiplied by size to set the Sprite2D scale, the collision 
# ğŸ‘‡ radius, and so on.
var scale_factor = 0.2

signal exploded

# ğŸ‘‡ calculate the correct collision size based on the rockâ€™s size.
# ğŸ‘‡ position and size are already in use as class variables, so you have to use an underscore for 
# ğŸ‘‡ the functionâ€™sarguments to prevent conflict.
func start(_position, _velocity, _size):
	position = _position
	size = _size
	mass = 1.5 * size
	# print_debug(($Image.scale) == null)
	# ğŸ‘‡ Sprite2D changed to Image to get rid of nullptr error.
	# ğŸ‘‡ Seems like an Godot issue and renaming the name of the
	# ğŸ‘‡ node in the scene tree solves it as done here.
	$Image.scale = Vector2.ONE * scale_factor * size
	radius = int($Image.texture.get_size().x / 2 * scale_factor * size)
	var shape = CircleShape2D.new()
	shape.radius = radius
	$CollisionFrame.shape = shape
	linear_velocity = _velocity
	angular_velocity = randf_range(-PI, PI)
	#ğŸ‘‡ This will ensure the explosion is scaled to match the rockâ€™s size.
	$Explosion.scale = Vector2.ONE * 0.75 * size

# Weâ€™re using physics_state for the parameter name rather than the default of state.
# This is to avoid confusion ğŸ‘‡ since state is already being used to track the playerâ€™s state.
func _integrate_forces(physics_state: PhysicsDirectBodyState2D) -> void:
# To implement the wrap-around effect (teleport player from one end of screen to other end)
	var xform = physics_state.transform
	# The wrapf() function takes a value (the first argument) and â€œwrapsâ€ it between any min/max values 
	# you choose. So, if the value goes below 0, it becomes screensize.x, and vice versa.
	xform.origin.x = wrapf(xform.origin.x, 0 - radius, screensize.x + radius)
	# Including the rock's radius in calculationâ˜ï¸ğŸ‘‡ results in smoother looking teleportation
	xform.origin.y = wrapf(xform.origin.y, 0 - radius, screensize.y + radius)
	physics_state.transform = xform

func explode():
	# ğŸ‘‡ hide the rock 
	$CollisionFrame.set_deferred("disabled", true)
	$Image.hide()
	# ğŸ‘‡ and play the explosion
	$Explosion/AnimationPlayer.play("explosion")
	$Explosion.show()
	exploded.emit(size, radius, position, linear_velocity)
	# ğŸ‘‡ Resetting Rigidbody variables.
	linear_velocity = Vector2.ZERO
	angular_velocity = 0
	# ğŸ‘‡ waiting for animation to finish before removing the rock.
	await $Explosion/AnimationPlayer.animation_finished
	queue_free()
